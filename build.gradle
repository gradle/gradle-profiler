plugins {
    id 'java'
    id 'groovy'
    id 'application'
}

repositories {
    jcenter()
    maven {
        url = 'https://repo.gradle.org/gradle/repo'
    }
    maven {
        url = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

configurations {
    profilerPlugins
}

dependencies {
    implementation 'org.gradle:gradle-tooling-api:5.2.1'
    implementation 'com.google.code.findbugs:annotations:3.0.1'
    implementation 'com.google.guava:guava:27.1-jre'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'com.typesafe:config:1.3.3'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'com.github.javaparser:javaparser-core:3.1.3'
    implementation 'org.apache.ant:ant-compress:1.5'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.openjdk.jmc:flightrecorder:7.0.0-SNAPSHOT'
    implementation 'com.android.tools.build:builder-model:3.0.0'

    profilerPlugins project(':chrome-trace')
    profilerPlugins project(':build-operations')

    runtimeOnly 'org.slf4j:slf4j-simple:1.7.10'
    testCompile 'org.codehaus.groovy:groovy:2.4.7'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testRuntime 'cglib:cglib:3.2.6'
    testRuntime 'org.objenesis:objenesis:2.6'
}

sourceCompatibility = 1.8
mainClassName = "org.gradle.profiler.Main"

processResources {
    into('META-INF/jars') {
        from configurations.profilerPlugins
    }
}
